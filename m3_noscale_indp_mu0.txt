model {
  
  for(k in 1:K){
    for(i in 1:N){
  fullData[i,k] ~ dnorm( (intercept[k] + alpha[i] + x[i]*beta[k] + gamma[k]*pscore[i] ),invsigma[k]);
    }
  }
  for(i in 1:N){
    alpha[i] ~ dnorm(0, exp(-2*logsigma_alpha));
  }
  
  for (k in 1:K){
    intercept[k] ~ dnorm(0,precision_prior_intercept);
    logsigma[k] ~ dnorm(0,precision_prior_logsigma);
    gamma[k] ~dnorm(0,precision_prior_gamma)
    beta[k] ~dnorm(0,PrecisionM[IndA[k]])
    
    IndA[k] ~ dcat(PInd[k,1:2])
    Ind[k] <- IndA[k]-1
    invsigma[k] <- exp(-2*logsigma[k])
  }
  
  PrecisionM[1] <- g/exp(2*logtau); PrecisionM[2] <- exp(-2*logtau)
  logsigma_alpha ~ dnorm(0, precision_prior_logsigmaAlpha);
  logtau ~ dnorm(0, precision_prior_logtau);

  PInd[1:K,2] <- pSignificant;      PInd[1:K,1] <- 1-PInd[1:K,2]
}